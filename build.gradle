// Gradle repositories and dependencies
buildscript
{
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
		classpath 'org.ajoberstar:grgit:1.4.+'
		classpath 'org.spongepowered:mixingradle:0.3-SNAPSHOT'
	}
}

plugins {
	id "java"
	id "com.github.hierynomus.license" version "0.11.0"
	id "eclipse"
	id "idea"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

idea { module { inheritOutputDirs = true } }

defaultTasks 'licenseFormat', 'build'

// Minimum version of Java required
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mixin {
	add sourceSets.main, "cubicchunks.mixins.refmap.json"
	//add sourceSets.main,
}

repositories {
	mavenCentral()
	maven {
		name = 'sponge'
		url = 'http://repo.spongepowered.org/maven'
	}
}

ext {
	git = org.ajoberstar.grgit.Grgit.open(file('.'))

	//used for license header
	projectName = 'Cubic Chunks Mod'
	inceptionYear = '2015'
}

group = "cubichunks"
archivesBaseName = "CubicChunks"
version = getModVersionAndWriteToFile("${git.describe()}")

minecraft {
	version = "1.9-12.16.1.1891"
	runDir = "run"

	// the mappings can be changed at any time, and must be in the following format.
	// snapshot_YYYYMMDD   snapshot are built nightly.
	// stable_#            stables are built at the discretion of the MCP team.
	// Use non-default mappings at your own risk. they may not always work.
	// simply re-run your setup task after changing the mappings to update your workspace.
	mappings = "snapshot_20160508"
	// makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

	replace "@@VERSION@@", project.version
	replaceIn "cubicchunks/CubicChunks.java"

}

reobfJar {
	addExtraSrgLine "PK: org/mapdb cubicchunks/org/mappdb"
	addExtraSrgLine "PK: com/flowpowered cubicchunks/com/flowpowered"
}

jar {
	exclude 'LICENSE.txt'
}

// Project repositories
repositories {
	mavenCentral()
	maven
	{
		name 'sonatype'
		url 'https://oss.sonatype.org/content/groups/public/'
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	shade 'com.flowpowered:flow-noise:1.0.1-SNAPSHOT'
	shade 'org.mapdb:mapdb:1.0.7'
	testCompile 'junit:junit:4.11'

	shade('org.spongepowered:mixin:0.5.5-SNAPSHOT') {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
	}
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


// License header formatting
license {
	ext.project = projectName
	ext.year = inceptionYear
	exclude "**/*.info"
	exclude "**/*.json"
	exclude "assets/*"
	header file('HEADER.txt')
	ignoreFailures false
	strictCheck true
	mapping {
		java = 'SLASHSTAR_STYLE'
	}
}

jar {
	manifest {
		attributes 'FMLAT': 'cubicchunks_at.cfg'
		attributes 'FMLCorePlugin': 'cubicchunks.asm.CoreModLoadingPlugin', 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
		attributes 'TweakOrder': '0'
		attributes 'ForceLoadAsMod': 'true'
	}
	configurations.shade.each { dep ->
		from(project.zipTree(dep)){
			exclude 'META-INF', 'META-INF/**'
		}
	}
}

//returns version string according to this: http://mcforge.readthedocs.org/en/latest/conventions/versioning/
//format: MCVERSION-MAJORMOD.MAJORAPI.MINOR.PATCH(-final/rcX/betaX)
//rcX and betaX are not implemented yet
private String getModVersion(String describe) {
	final String baseVersionRegex = "v[0-9]+\\.[0-9]+";
	final String unknownVersion = String.format("%s-UNKNOWN_VERSION%s", mcVersion, versionSuffix);
	if (!describe.contains('-')) {
		//is it the "vX.Y" format?
		if (describe.matches(baseVersionRegex)) {
			return String.format("%s-%s.0.0%s", mcVersion, describe, versionSuffix);
		}
		return unknownVersion;
	}
	//Describe format: vX.Y-build-hash
	String[] parts = describe.split("-");
	if (!parts[0].matches(baseVersionRegex)) {
		return unknownVersion;
	}
	if (!parts[1].matches("[0-9]+")) {
		return unknownVersion;
	}
	String mcVersion = mcVersion;
	String modAndApiVersion = parts[0].substring(1);
	//next we have commit-since-tag
	int commitSinceTag;
	try {
		commitSinceTag = Integer.parseInt(parts[1]);
	} catch (NumberFormatException ex) {
		ex.printStackTrace();
		return unknownVersion;
	}

	int minor;
	int patch;
	int minorFreeze = -1;
	if (!(versionMinorFreeze == null || versionMinorFreeze.isEmpty())) {
		try {
			minorFreeze = Integer.parseInt(versionMinorFreeze);
		} catch (NumberFormatException ex) {
			ex.printStackTrace();
		}
	}
	if (minorFreeze < 0) {
		minor = commitSinceTag;
		patch = 0;
	} else {
		minor = minorFreeze;
		patch = commitSinceTag - minorFreeze;
	}
	String version = String.format("%s-%s.%d.%d%s", mcVersion, modAndApiVersion, minor, patch, versionSuffix);
	return version;
}
private String getModVersionAndWriteToFile(String describe) {
	String version = getModVersion(describe)
	//write to file
	File file = new File("VERSION");
	PrintWriter pw = null;
	try {
		if(file.exists()) {
			file.delete();
		}
		file.createNewFile();
		pw = new PrintWriter(new FileOutputStream(file));
		pw.println("VERSION=" + version);
	} catch(IOException ex) {
		ex.printStackTrace();
	} finally {
		if(pw != null) {
			try{pw.close()}catch(IOException ex){}
		}
	}

	return version;
}
